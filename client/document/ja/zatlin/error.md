## エラーについて
Zatlin を実行したときに発生するエラーには、文法エラーと意味論的エラーと実行時エラーの 3 種類があります。

文法エラーは、入力された Zatlin コードに文法に反した記述が含まれていた場合に発生します。
また、意味論的エラーは、入力された Zatlin コードに何らかの理由によって実行できない記述が含まれていた場合に発生します。
これらの 2 つの種類のエラーは入力された Zatlin コードによるものなので、そのコードを Zatlin 処理系に実際に渡すことで、前もってコードにエラーが含まれていないかどうか確認できます。
一方で、実行時エラーは、入力された Zatlin コードに従って文字列を生成している最中に予期しない現象が起こった場合に発生します。

意味論的エラーと実行時エラーには、エラーの種類に応じてエラーコードが設定されています。
エラーメッセージの最初に表示される 4 桁の数値がそのエラーコードです。

ZpDIC Online では、辞書に Zatlin コードを設定しておくことで単語編集画面で Zatlin を実行することができますが、そのときに発生するエラーは明示的には報告されません。
そのため、Zatlin コードの設定画面で実際に Zatlin を実行して、エラーが起きないかどうかを確認しておくことをお勧めします。

## 文法エラー
文法エラーは `PARSING FAILED` というメッセージとともに報告されます。
エラー箇所が表示されるので、その箇所の構文の誤りを修正してください。
```zatlin-try
% "foo" 2 | "bar" 3 1;  # 重みが 2 回書かれているのでエラー
```

## 意味論的エラー
### #1000: There is no main pattern
メインパターンが定義されていません。
Zatlin の処理系はメインパターンに沿って文字列を出力するため、必ずメインパターンを書く必要があります。
```zatlin-try
vowel = "a" | "e" | "i";
# メインパターンがない
```

### #1001: There are multiple main patterns
メインパターンが複数個定義されています。
メインパターンは 1 つのみ定義してください。
```zatlin-try
vowel = "a" | "e" | "i";
% vowel vowel;
cons = "s" | "t" | "k";
% cons vowel;  # メインパターンの 2 つ目の定義
```

### #1100: Unresolved identifier
定義されていない変数が使われています。
該当の変数の定義を追加してください。
```zatlin-try
vowel = "a" | "e" | "i";
% cons vowel;  # cons が定義されていない
```

### #1101: Circular reference involving identifier
変数定義に循環参照が含まれます。
変数定義に循環参照が含まれていると、実行時に無限ループになり処理が永遠に終わらなくなる可能性があるので、Zatlin は前もってこれをエラーとして報告します。
循環参照がなくなるように定義を修正してください。
```zatlin-try
vowel = "a" | "e" | "i";
semivowel = "y" (vowel | diphthong);
diphthong = semivowel vowel;
% diphthong;  # diphthong → semivowel → diphthong で循環
```

ただし、メインパターンから文字列を生成するときに全く使われない変数の間での循環参照は、このエラーの対象にはなりません。

### #1102: Duplicate definition of identifier
同名の変数が複数回定義されています。
片方の変数定義を削除するか名前を変更してください。
```zatlin-try
vowel = "a" | "e" | "i";
semivowel = "y" vowel;
vowel = "o" | "u";  # vowel の 2 回目の定義
% semivowel vowel;
```

### #1103
このエラーコードは現在使われていません。

### #1104: Total weight is zero
`|` による選択パターンに指定された重みの合計が 0 になっている箇所があります。
重みが 0 であるということはそのパターンが絶対に選択されないという意味なので、全てのパターンの重みが 0 の場合は生成に使うパターンの選択ができず、エラーとして報告されます。
該当箇所の重みの少なくとも 1 つに 0 以外の値を指定してください。
```zatlin-try
vowel = "a" 0 | "e" 0 | "i" 0;  # 重みの合計が 0
% vowel;
```

### #1105: Index of backreference is invalid
後方参照パターンが不正な位置にあります。
例えば、`&2` は 2 番目のパターンを参照するので、`&2` 自身は 3 番目以降に置かれている必要があります。
したがって、`&2` が 1 番目もしくは 2 番目に置かれていた場合、このエラーが発生します。
```zatlin-try
vowel = "a" | "e" | "i";
% &2 vowel;  # &2 が 1 番目に置かれている
```

## 実行時エラー
### #2000: Possibly empty
複合パターンから文字列を生成しようとした際に、除外パターンに合致しない文字列が生成できなかったときに発生します。
以下の例では、複合パターンの選択パターン部分が生成し得る文字列が全て除外されてしまっているため、文字列の生成に失敗してこのエラーが発生します。
```zatlin-try
% "ab" | "ac" | "bc" - ^ "a" | "c" ^;
# 選択パターンが生成し得る文字列は〈ab〉か〈ac〉か〈bc〉
# これらは全て〈a〉で始まるか〈c〉で終わるかしているので除外されている
```
Zatlin 処理系は、除外パターンに合致しない文字列が生成されるまで、最大で 100 回まで選択パターン部分から文字列を生成し直します。
そのため、除外されない文字列が選択パターン部分から理論的に生成され得る場合でも、その確率が非常に低い場合はこのエラーが発生することがあります。
```zatlin-try
% "a" 1000000 | "b" 1 - "a";
# 理論的には〈b〉が生成され得るが確率は非常に低い (1/1000001 の確率)
# そのためほとんど確実に生成に失敗する
```

### #2001: No such identifier
実行する変数名を指定して Zatlin を実行したときに、該当する名前の変数が定義されていなかったときに発生します。
ZpDIC Online ではこの機能を利用していないので、発生することはありません。

## その他のエラー
上記以外に、9000 番台のエラーコードをもつエラーが発生する可能性があります。
これは、Zatlin が意図通りに動作していれば絶対に起こり得ないエラーなので、もしこれが発生した場合は Zatlin のバグであるということになります。
これが発生した場合は、開発者に連絡してください。